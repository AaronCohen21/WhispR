plugins {
    id 'java'
}

group 'com.aaroncohen'
version '1.0.1'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'com.formdev:flatlaf:2.0.1'
    implementation 'com.intellij:forms_rt:7.0.3'
    implementation "net.coobird:thumbnailator:[0.4, 0.5)"
}

jar {
    manifest {
        attributes(
                'Main-Class': 'com.aaroncohen.WhispR.WhispR',
        )
    }
}

task fatJar(type: Jar) {
    manifest.from jar.manifest
    duplicatesStrategy = 'include'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}

task jpackageMac {
    doLast {
        exec {
            workingDir '.'
            commandLine 'sh', '-c', '$(/usr/libexec/java_home)/bin/jpackage --input build/libs/ \
                --app-version 1.0.1 \
                --main-jar WhispR-1.0.1.jar \
                --icon "src/main/resources/appicon/WhispR.icns" \
                --name WhispR \
                --dest build/dist/ \
                --vendor "Aaron Cohen" \
                --copyright "Made By Aaron Cohen" \
                --description "A lightweight Java chat app I made for fun because I got really bored one day" \
                --main-class com.aaroncohen.WhispR.WhispR'
        }
    }
}